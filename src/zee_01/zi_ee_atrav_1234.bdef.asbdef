 managed;
 with draft;

 define behavior for ZI_EE_ATRAV_1234 alias Travel
 implementation in class zbp_i_ee_travel_1234 unique
 persistent table zee_atrav_1234 // gerçek tablo
 draft table zeerap_dtrav_1 // geçici ara tablo
 lock master total etag LastChangedAt
 etag master LocalLastChangedAt
 {
   create;
   update;
   delete;
   association _Booking { create; with draft; }

   field ( numbering : managed, readonly ) TravelUUID;
   field ( readonly ) TravelId, TotalPrice, TravelStatus;
   field ( readonly ) LastChangedAt, LastChangedBy, CreatedAt, CreatedBy, LocalLastChangedAt;
   field ( mandatory ) AgencyID, CustomerID;

   action ( features : instance ) acceptTravel result [1] $self;
   action ( features : instance ) rejectTravel result [1] $self;
   internal action recalcTotalPrice;

   determination setInitialStatus on modify { create; }
   determination calculateTotalPrice on modify { field BookingFee, CurrencyCode; }
   determination calculateTravelID on save { create; }

   mapping for zee_atrav_1234
     {
       TravelUUID         = travel_uuid;
       TravelID           = travel_id;
       AgencyID           = agency_id;
       CustomerID         = customer_id;
       BeginDate          = begin_date;
       EndDate            = end_date;
       BookingFee         = booking_fee;
       TotalPrice         = total_price;
       CurrencyCode       = currency_code;
       Description        = description;
       TravelStatus       = overall_status;
       CreatedBy          = created_by;
       CreatedAt          = created_at;
       LastChangedBy      = last_changed_by;
       LastChangedAt      = last_changed_at;
       LocalLastChangedAt = local_last_changed_at;
     }
 }

 define behavior for ZI_EE_abook_1234 alias Booking
 implementation in class zbp_i_ee_booking_1234 unique
 persistent table zee_abook_1234  // gerçek tablo
 draft table zeerap_dbook_1 // geçici tablo
 lock dependent by _Travel
 etag master LocalLastChangedAt
 {
   update;
   delete;

   association _Travel { with draft; }

   field ( numbering : managed, readonly ) BookingUUID;
   field ( readonly ) TravelUUID, BookingId;
   field ( readonly ) CreatedBy, LastChangedBy, LocalLastChangedAt;
   determination calculateBookingID on modify { create; }
   determination calculateTOtalPrice on modify { field FlightPrice, CurrencyCode; }
   mapping for zee_abook_1234
     {
       BookingUUID        = booking_uuid;
       TravelUUID         = travel_uuid;
       BookingID          = booking_id;
       BookingDate        = booking_date;
       CustomerID         = customer_id;
       CarrierID          = carrier_id;
       ConnectionID       = connection_id;
       FlightDate         = flight_date;
       FlightPrice        = flight_price;
       CurrencyCode       = currency_code;
       CreatedBy          = created_by;
       LastChangedBy      = last_changed_by;
       LocalLastChangedAt = local_last_changed_at;
     }
 }